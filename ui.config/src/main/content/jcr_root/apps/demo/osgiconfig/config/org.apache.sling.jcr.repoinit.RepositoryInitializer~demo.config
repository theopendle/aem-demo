scripts=["

create path (sling:OrderedFolder) /content/dam/demo
create path (nt:unstructured) /content/dam/demo/jcr:content

set properties on /content/dam/demo/jcr:content
    set cq:conf{String} to /conf/demo
    set jcr:title{String} to Demo
end

# Create the authoring group for which 4-eyes validation will apply
create group demo-author with path demo
add demo-author to group content-authors
add demo-author to group workflow-users

# Remove replication permission on /content
set ACL for demo-author
    deny crx:replicate on /content
end

# Create folder to hold exclusion groups
create path (rep:AuthorizableFolder) /home/groups/demo/exclusion

# Create the service user used by the user service
create service user demo-svc-user-management
set ACL for demo-svc-user-management
    allow jcr:read on /home/groups/demo
    allow jcr:read on /home/users/demo
    allow jcr:all on /home/groups/demo/exclusion
end

# Create some test users
create user demo-author-user1 with path demo with password demo-author-user1
create user demo-author-user2 with path demo with password demo-author-user2
create user demo-author-user3 with path demo with password demo-author-user3
add demo-author-user1,demo-author-user2,demo-author-user3 to group demo-author

"]


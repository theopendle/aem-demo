scripts=["

create path (sling:OrderedFolder) /content/dam/demo
create path (nt:unstructured) /content/dam/demo/jcr:content

set properties on /content/dam/demo/jcr:content
    set cq:conf{String} to /conf/demo
    set jcr:title{String} to Demo
end

# Create folder to hold exclusion groups
create path (rep:AuthorizableFolder) /home/groups/demo/exclusion

# Create the service user used by the user service
create service user demo-svc-user-management
set ACL for demo-svc-user-management
    allow jcr:read on /home/groups/demo
    allow jcr:read on /home/users/demo
    allow jcr:all on /home/groups/demo/exclusion
end

# Create the authoring group for which 4-eyes validation will apply
create group hr with path demo
add hr to group content-authors
add hr to group workflow-users

# Create the validator group for which 4-eyes validation will apply
create group sales with path demo
add sales to group workflow-users

# Remove replication permission on /content
set ACL for hr,sales
    deny crx:replicate on /content
end

# Create some test users
create user allan with path demo with password allan
create user betty with path demo with password betty
create user charlie with path demo with password charlie
add allan,betty,charlie to group hr

create user debra with path demo with password debra
create user evan with path demo with password evan
create user fiona with path demo with password fiona
add debra,evan,fiona to group sales

"]

